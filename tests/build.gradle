import org.apache.tools.ant.taskdefs.Manifest

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath 'org.apache.ant:ant:1.10.7'
    classpath files('../gradle-plugin/build/libs/gradle-plugin-0.99')
  }
}

plugins {
  id 'java'
  id 'com.needhamsoftware.unojar' version '0.99.1-SNAPSHOT'
}

version '0.99.1-SNAPSHOT'

sourceCompatibility = 1.11

repositories {
  mavenLocal()
  mavenCentral()
}

configurations {
  ujconf
}

dependencies {
  implementation 'com.needhamsoftware.unojar:core:' + project.version
  ujconf project(path: ':ant', configuration: 'antjar')

  testCompile group: 'junit', name: 'junit', version: '4.12'
}

task libtestJar(type: Jar) {
  dependsOn compileTestJava
  from('build/classes/java/test') {
    include '**/LibTest.class'
  }
  archiveName "libtest.jar"
}

unojar {
  unoJar "build/testJar2.jar"
  manifestAttrs ("Test-Attribute" : "TestValue")
  appMainClass "com.needhamsoftware.unojar.TestMain"
  appFiles {
    fileset(dir: 'build/classes/java/test') {
      include(name: '**/TestMain.class')
    }
  }
  depLibs {
    fileset(dir: 'build/libs') {
      include(name: 'libtest.jar')
    }
  }
}

task testJar1(dependsOn: [testClasses, libtestJar, ':ant:jar']) {
  def cp = project.buildscript.configurations.getByName('classpath')
  def find = cp.find {
    if (it.canonicalPath.matches('.*needhamsoftware/unojar/ant/.*/ant-.*\\.jar')) return it
  }
  def ujjar = new URL('file://' + find)
  def p = ujjar.toURI().toString().substring(5)
  println p
  println '' + ujjar + ' ' + new File(p).exists()

  doLast {
    ant.taskdef(name: "uj", classpath: ujjar, classname: "com.needhamsoftware.unojar.ant.UnoJarTask")

    def mf = Manifest.getDefaultManifest()
    mf.addConfiguredAttribute(new Manifest.Attribute("Uno-Jar-Main-Class", "com.needhamsoftware.unojar.TestMain"))
    mkdir("$projectDir/build/com.needhamsoftware.unojar/" + name)
    def mff = new File("$projectDir/build/com.needhamsoftware.unojar/" + name + "/manifest.mf")
    mff.write "" + mf;

    ant.uj(destFile: 'build/testjar1.jar', manifest: mff) {

      main {
        fileset(dir: 'build/classes/java/test') {
          include(name: '**/TestMain.class')
        }
      }
      lib {
        fileset(dir: 'build/libs') {
          include(name: 'libtest.jar')
        }
      }
      // note that we can't use a mainfest {} closure here because gradle creates a Gradle Manifest not an Ant
      // manifest and then proceeds to throw it away. Many hours died to bring us this information...
    }
  }
}

packUnoJar.dependsOn libtestJar
test.dependsOn testJar1
test.dependsOn testJar2
test.dependsOn packUnoJar