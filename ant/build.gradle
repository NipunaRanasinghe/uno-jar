import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'com.palantir.git-version' version '0.12.2'
}

sourceCompatibility = 1.11
version = '0.99-SNAPSHOT'

dependencies {
    compile project(':core')
    compile 'org.apache.ant:ant:1.10.1'
}

jar {
    def gitDetails = versionDetails();
    setArchiveFileName 'uno-jar-ant.jar'
    manifest {
        attributes(
                'Built-By'       : System.properties['user.name'],
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Revision' : version + ' ('+gitDetails.gitHashFull+')' + (gitDetails.isCleanTag ? '' : '(with uncommitted files)') ,
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
    from('' + projectDir + "/../core/build/libs") {
        include "core.jar"
    }

}

task copyCoreJar (type:Copy) {
    from '' + projectDir + "/../core/build/libs/core.jar"
    into '' + projectDir + "/build/classes/java/main/com/needhamsoftware/unojar/ant"
}

jar.dependsOn(copyCoreJar)

configurations {
    antjar
}

artifacts {
    antjar jar
}